---
title: P4入门
toc: true
thumbnail: 'https://pic.downk.cc/item/5f43162f160a154a67c02d43.png'
comments: true
tags:
  - P4
  - SDN
date: 2020-08-23 04:20:16
urlname:
categories:
---

P4是一门封包处理器描述语言：

- 无协议protocol限制

  可以很有弹性的处理任何协议

- 无目标target限制

  可以描述不同的目标，从高速的ASIC到软件交换机

- 可重新组态的网络

  可以在部署好网络之后再重新定义

![](https://pic.downk.cc/item/5f43162f160a154a67c02d43.png)

# Parser

Parser包含两部分：

### Header

Header代表了哪些格式的Header可以使用，例如Ethernet header。

例如Ethernet定义：

```json
header_type ethernet_t{
	fields{
  	dst_addr : 48;
  	src_addr : 48;
  	ether_type : 16;
}
}

```

里面数字都是以bit定义的。

使用需要实例化：

```json
header ethernet_t ethernet;
```

P4-16

```
header EthernetHeader {

    bit<48> dst;
    bit<48> src;
    bit<16> etherType;

}

EthernetHeader ethernetHeader;
```



### Parser

Header之后就要定义Parser，一般都有一个开始的parser：

```json
parser start{
  return next_parser;
}
```

在每一个parser中都会依据目前所分析的内容决定下一个parser，直到回传的内容是ingress，或者其他control function，以ipv4为例：

```json
parser start{
  return parser_ethernet;
}

parser parser_ethernet{
  extract(ethernet);
  return select(ethernet.ether_type){
    0x0800 : parse_ipv4;
    default : ingress;
  }
}

parser parser_ipv4{
  extract(ipv4);
  return ingress;
}
```

1. extract : 将packet以特定的header取出来，取出来的资料长度以header定义的为主。

2. retrun：透过return决定要往哪个parser或者control function，可以select或者return。

3. select(select_exp)：像c中switch-case，依据特定field值，取决定去那个parser。

4. select_exp：依据spec，可以是

   field_ref : 如 ethernet.ether_type

   latest.field_name : 以最後 extract 的 header 為主，取用他的 field

   current (offset, length) : 以目前的 packet offset 位置開始某固定長度所取得的數值

在 egress 階段開始時會先對 packet 做 deparsing 的動作，亦即將 ingress 所做出的修改（add header、modiffy header等）重新封裝回 packet 當中。

**P4-16**

與 1.0/1.1 版不同的是，原先的版本需要定義出很多個 parser，並在不同的 parser 間切換，而新版的則是定義一個 parser，並在 parser 中撰寫很多不同的狀態（state），並透過 accept 與 reject 決定 parser 是否成功解析一個封包。

有限状态机

```json
parser ParserName(packet_in pkt, ...) {

    state start {
        ....
    }

    state state_name {
        ....
    }
}
```

parser 所帶入的參數主要有：

- packet_in：執行期間所要解析的封包
- 其他輸出內容：可以是 struct 或是一般封包参照

不同的 state 切換可以透過 **transition** 這個關鍵字去做切換，而 transition 後面可以接 state 名稱或是 select 語法，我們以 Ethernet + IPv4 為例子。

```json
parser MyParser(packet_in pkt, out eth, out ipv4) {

    state start {
        transition eth;
    }

    state eth {
        pkt.extract(eth);
        transition select(eth.ethType) {
            0x800: ip;
            _: reject;
        }
    }

    state ip {
        pkt.extract(ipv4);
        transition accept;
    }

}
```

除了使用 transition, select 以及 extract 以外，Parser 還有提供 verify 語法，透過 verify 檢查，決定 parser 是否要丟出 error 並跳至 reject 狀態，使用方法如下：

```json
verify(判斷式, 錯誤類別);
```

# Table (Match + Action)

简单IPv4转发表的定义可以实现如下：

```
table routing_table {
    key = {
        hdr.ipv4.dstAddr: lpm;
    }
    actions = {
       ipv4_forward;
       drop;
       NoAction;
    }
    default_action = NoAction();
}
```

上面的routing_table读取IPv4目标IP地址，并根据最长前缀匹配算法进行匹配。然后，在符合规则的数据包上，可以执行三个操作：ipv4_forward，drop或NoAction。

# Deparser

它定义传出数据包的数据包标头顺序。

```
control deparser(packet_out packet,
                 in headers hdr) {

    apply {
        packet.emit(hdr.ethernet);
        packet.emit(hdr.ipv4);
    }
}
```

完整的ipv4转发定义：

```json
/* -*- P4_16 -*- */
#include <core.p4>
#include <v1model.p4>

const bit<16> TYPE_IPV4 = 0x0800;

typedef bit<9>  egressSpec_t;
typedef bit<48> macAddr_t;
typedef bit<32> ip4Addr_t;

header ethernet_t {
    macAddr_t dstAddr;
    macAddr_t srcAddr;
    bit<16>   etherType;
}

header ipv4_t {
    bit<4>    version;
    bit<4>    ihl;
    bit<8>    diffserv;
    bit<16>   totalLen;
    bit<16>   identification;
    bit<3>    flags;
    bit<13>   fragOffset;
    bit<8>    ttl;
    bit<8>    protocol;
    bit<16>   hdrChecksum;
    ip4Addr_t srcAddr;
    ip4Addr_t dstAddr;
}

struct headers {
    ethernet_t   ethernet;
    ipv4_t       ipv4;
}

struct routing_metadata_t {
    ip4Addr_t nhop_ipv4;
}

struct metadata {
    routing_metadata_t routing;
}

parser RouterParser(packet_in packet,
                    out headers hdr,
                    inout metadata meta,
                    inout standard_metadata_t standard_metadata) {

    state start {
        transition parse_ethernet;
    }

     state parse_ethernet {
        packet.extract(hdr.ethernet);
        transition select(hdr.ethernet.etherType) {
            TYPE_IPV4: parse_ipv4;
            default: accept;
        }
    }

    state parse_ipv4 {
        packet.extract(hdr.ipv4);
        transition accept;
    }

}

action drop() {
    mark_to_drop();
}

control ingress(inout headers hdr,
                inout metadata meta,
                inout standard_metadata_t standard_metadata) {

    action ipv4_forward(ip4Addr_t nextHop, egressSpec_t port) {
        meta.routing.nhop_ipv4 = nextHop;
        standard_metadata.egress_spec = port;
        hdr.ipv4.ttl = hdr.ipv4.ttl - 1;
    }

    table routing_table {
        key = {
            hdr.ipv4.dstAddr: lpm;
        }
        actions = {
            ipv4_forward;
            drop;
            NoAction;
        }
        default_action = NoAction();
    }

    apply {
        routing_table.apply();
    }

}

control egress(inout headers hdr,
               inout metadata meta,
               inout standard_metadata_t standard_metadata) {

    action set_dmac(macAddr_t dstAddr) {
        hdr.ethernet.dstAddr = dstAddr;
    }

    action set_smac(macAddr_t mac) {
        hdr.ethernet.srcAddr = mac;
    }

    table switching_table {
        key = {
            meta.routing.nhop_ipv4 : exact;
        }
        actions = {
            set_dmac;
            drop;
            NoAction;
        }
        default_action = NoAction();
    }

    table mac_rewriting_table {

        key = {
            standard_metadata.egress_port: exact;
        }

        actions = {
            set_smac;
            drop;
            NoAction;
        }

        default_action = drop();
    }

    apply {
        switching_table.apply();
        mac_rewriting_table.apply();
    }

}

control deparser(packet_out packet,
                 in headers hdr) {

    apply {
        packet.emit(hdr.ethernet);
        packet.emit(hdr.ipv4);
    }
}

control MyComputeChecksum(inout headers hdr, inout metadata meta) {
     apply {
	update_checksum(
	    hdr.ipv4.isValid(),
            { hdr.ipv4.version,
	      hdr.ipv4.ihl,
              hdr.ipv4.diffserv,
              hdr.ipv4.totalLen,
              hdr.ipv4.identification,
              hdr.ipv4.flags,
              hdr.ipv4.fragOffset,
              hdr.ipv4.ttl,
              hdr.ipv4.protocol,
              hdr.ipv4.srcAddr,
              hdr.ipv4.dstAddr },
            hdr.ipv4.hdrChecksum,
            HashAlgorithm.csum16);
    }
}

control MyVerifyChecksum(inout headers hdr, inout metadata meta) {
    apply {  }
}


V1Switch(
RouterParser(),
MyVerifyChecksum(),
ingress(),
egress(),
MyComputeChecksum(),
deparser()
) main;
```

---

# P4编译的个人理解

p4-hlir项目解析p4，可以看成p4语言的python解析器，生成对应的python对象。
基于这个解析结果可以再加上模板输出json或者c, 或者RTL，或者DPDK，或者NP的微码。。

BMv2可以看成是一个设备或者模拟器，p4c-bm就是调用p4-hlir解析p4，生成BMv2配置所需的json文件和远程调用接口。所以一个p4模型被mininet模拟的picture是这样的：

p4 -> p4-hlir -> p4c-bm -> BMv2 -> mininet

p4c-bm安装会把p4-hlir一并安装：
git clone https://github.com/p4lang/p4c-bm.git
cd p4c-bm
sudo pip install -r requirements_v1_1.txt
sudo python setup.py install
至此/usr/local/bin下面就有了p4开头的编译、校验、图片生成命令。

p4-validate用来检查p4语法

p4-shell simple_router.p4
这是python界面，h对象包含了HLIR已经解析出的对象，用法参见https://github.com/p4lang/p4-hlir

p4-graph，这个项目不见了，原来跑到这里。这个命令会在当前目录下生成几个图片来描述Table/parser关系。

p4c-bmv2
--json 生成P4的Json格式
--pd 生成 PD C++ code
--p4-v1.1 if your input program is a P4 v1.1 program

进入p4-factory的targets/simple_router/p4src，编译一下p4模型:
p4c-bmv2 --json JSON simple_router.p4
这时候JSON文件就生成了，打开看看是不是很眼熟？
1. metadata跟header_type放在一起
2. headers成员里面有个metadata:true/false来标记
3. headers里面还有standard_metadata
4. 还有parsers/actions组。。。

至于怎么用BMv2或者mininet跑起来请看下一篇

如果你有自己的设备，CPU/FPGA/NP或者其它想支持P4的设备，可以用p4-hlir用python解析生成代码，或者基于p4c-bmv2输出的JSON生成代码或者执行文件。

BMv2j记得不是生成代码，而是加载JSON作为配置，如果代码效率要求不高应该可以这么用。

BTW，P4这个名字本来是Perforce的缩写，一个版本管理工具，P4全名是Programming Protocol independent Packet Processing, PPiPP更唯一